plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '1.1.0'
}

archivesBaseName = project.archives_base_name
version = project.plugin_version
group = project.maven_group
description = 'A plugin to aid in the development and publishing of Minecraft mods!'

repositories {
    mavenCentral()
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
    // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
    // We'll use that if it's available, but otherwise we'll use the older option.
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release.set(targetVersion)
    } else {
        it.sourceCompatibility = JavaVersion.toVersion(targetVersion)
        it.targetCompatibility = JavaVersion.toVersion(targetVersion)
    }
}


gradlePlugin {
    website = 'https://github.com/dexman545/Outlet'
    vcsUrl = 'https://github.com/dexman545/Outlet'

    plugins {
        outlet {
            //noinspection GroovyAssignabilityCheck
            id = 'io.github.dexman545.outlet'
            //noinspection GroovyAssignabilityCheck
            implementationClass = 'dex.plugins.OutletPlugin'
            displayName = 'Outlet'
            description = 'A plugin to aid in the development and publishing of Minecraft mods!'
            tags.set(['publish', 'mods', 'minecraft'])
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    implementation 'com.github.yuchi:npm-semver:1.0.0'
    implementation 'net.fabricmc:fabric-loader:0.16.14'
    implementation "org.ow2.asm:asm:${project.asm_version}"
    implementation "org.ow2.asm:asm-analysis:${project.asm_version}"
    implementation "org.ow2.asm:asm-commons:${project.asm_version}"
    implementation "org.ow2.asm:asm-tree:${project.asm_version}"
    implementation "org.ow2.asm:asm-util:${project.asm_version}"
    implementation 'org.apache.commons:commons-configuration2:2.10.1'
}

test {
    useJUnitPlatform()
}